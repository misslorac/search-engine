#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.


AC_PREREQ(2.59)

AC_INIT(src/include/Logger.h)
ALOG_MAJOR_VERSION=1
ALOG_MINOR_VERSION=1
ALOG_MICRO_VERSION=0
ALOG_RELEASE=1
ALOG_VERSION=$ALOG_MAJOR_VERSION.$ALOG_MINOR_VERSION.$ALOG_MICRO_VERSION
LT_MAJOR=0
LT_REVISION=0
LT_AGE=0
ALOG_TOP_BUILD_DIR=`pwd`
AC_SUBST(LT_MAJOR)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)
AC_SUBST(ALOG_MAJOR_VERSION)
AC_SUBST(ALOG_MINOR_VERSION)
AC_SUBST(ALOG_MICRO_VERSION)
AC_SUBST(ALOG_RELEASE)
AC_SUBST([USER])
AC_SUBST([HOSTNAME])
AC_SUBST([ALOG_TOP_BUILD_DIR])

AC_CONFIG_AUX_DIR(.)
AM_INIT_AUTOMAKE(alog, $ALOG_VERSION)
AC_CONFIG_SRCDIR([src/cpp/Properties.cpp])
AC_CONFIG_HEADER([config.h])

# Default prefix path
# AC_PREFIX_DEFAULT(~)

# Checks for programs.
${CXXFLAGS="-O3 -Wall"}
AC_PROG_CXX
AC_PROG_CC

# Checks for libraries.
# FIXME: Replace `main' with a function in `-llog4cxx':
# AC_CHECK_LIB([log4cxx], [main])
# FIXME: Replace `main' with a function in `-lpthread':
AC_CHECK_LIB([pthread], [main])
AC_CHECK_LIB([z], [gzwrite])
AC_PROG_LIBTOOL

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h sys/time.h syslog.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

# Checks for library functions.
AC_CHECK_FUNCS([gettimeofday memset])

trylib=""
AC_ARG_WITH(zlib, 
            [  --with-zlib=PATH     Specify path to zlib installation. Default /usr/local ],
            [
             if test "x$withval" != "xno" ; then
                 trylib=$withval
             fi
             ]
            )

AC_CACHE_CHECK([for zlib directory], ac_cv_zlib_dir, [
                saved_LIBS="$LIBS"
                saved_LDFLAGS="$LDFLAGS"
                saved_CPPFLAGS="$CPPFLAGS"
                found=no
                for sdir in $trylib "" $prefix /usr/local ; do
                    LDFLAGS="$saved_LDFLAGS"
                    LIBS="$saved_LIBS -lz"

                    # Skip the directory if it isn't there.
                    if test ! -z "$sdir" -a ! -d "$sdir" ; then
                        continue;
                    fi
                    if test ! -z "$sdir" ; then
                        if test -d "$sdir/lib" ; then
                            LDFLAGS="-L$sdir/lib $LDFLAGS"
                        else
                            LDFLAGS="-L$sdir $LDFLAGS"
                        fi
                        if test -d "$sdir/include" ; then
                            CPPFLAGS="-I$sdir/include $CPPFLAGS"
                        else
                            CPPFLAGS="-I$sdir $CPPFLAGS"
                        fi
                    fi
                    AC_TRY_LINK([#include <zlib.h>], [ zlibVersion() ], [ zlib_linked=yes ], [ zlib_linked=no 
                                 ])
                    if test $zlib_linked = yes; then
                        if test ! -z "$sdir" ; then
                            ac_cv_zlib_dir=$sdir
                        else
                            ac_cv_zlib_dir="(system)"
                        fi
                        found=yes
                        break
                    fi
                done
                LIBS="$saved_LIBS"
                LDFLAGS="$saved_LDFLAGS"
                CPPFLAGS="$saved_CPPFLAGS"
                if test $found = no ; 
                then
                    AC_MSG_ERROR([zlib is required. If it's already installed, specify its path using --with-zlib=/dir/])
                    fi
                    ])
LIBS="-lz $LIBS"
if test $ac_cv_zlib_dir != "(system)"; then
    if test -d "$ac_cv_zlib_dir/lib" ; then
        LDFLAGS="-L$ac_cv_zlib_dir/lib $LDFLAGS"
        zlibroot="$ac_cv_zlib_dir/lib"
    else
        LDFLAGS="-L$ac_cv_zlib_dir $LDFLAGS"
        zlibroot="$ac_cv_zlib_dir"
    fi
    if test -d "$ac_cv_zlib_dir/include" ; then
        CPPFLAGS="-I$ac_cv_zlib_dir/include $CPPFLAGS"
    else
        CPPFLAGS="-I$ac_cv_zlib_dir $CPPFLAGS"
    fi
fi

AC_CONFIG_FILES([Makefile
                 src/cpp/Makefile
                 src/example/Makefile
                 src/test/Makefile
                 src/test/test.h
                 src/test/t1.conf
                 src/test/t2.conf
                 src/test/t3.conf
                 src/test/t4.conf
                 src/test/t5.conf
                 src/test/t6.conf
                 src/test/t7.conf
                 src/test/t8.conf
                 src/test/t9.conf
                 src/test/t10.conf
                 src/test/t11.conf
                 src/test/t12.conf
				 alog.spec
				 Doxyfile])
AC_OUTPUT
