#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.24 2012/03/29 03:18:23 hightman Exp $
# scws-1.x.x configure autoconf script
#

# startup
AC_PREREQ(2.59)

AC_INIT(scws, 1.2.0, [http://www.ftphp.com/scws])
SHARED_LIB_VERSION=2:0:1

AM_INIT_AUTOMAKE

dnl copy right
AC_COPYRIGHT([Copyright (C)2007, 2008 hightman
This package maybe copied, distributed and modified under
the terms of my License; see COPYING for more details])

dnl identify the source code tree
dnl AC_CONFIG_SUBDIRS([libscws cli])

dnl output config header
AM_CONFIG_HEADER([config.h])

# Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_DISABLE_STATIC
AC_PROG_LIBTOOL
AC_PROG_INSTALL

# libtool
LIBTOOL="$LIBTOOL --preserve-dup-deps"
AC_SUBST(LIBTOOL)

dnl developer flags
AC_ARG_ENABLE(developer, AC_HELP_STRING([--enable-developer],
              [Compile with more warnings and debugging symbols]),
              [
                enable_developer=yes
                AC_DEFINE(HAVE_NOT_QUIET,,[libscws used as quiet mode])
              ], [ enable_developer=no ] )
if test "$enable_developer" = "no" ; then
  tmp_cflags=`echo $CFLAGS | sed 's/-O//g'`	
  if test "$tmp_cflags" = "$CFLAGS"; then  CFLAGS="$CFLAGS -O2"; fi
else
  tmp_cflags=`echo $CFLAGS | sed 's/-g//g'`	
  if test "$tmp_cflags" = "$CFLAGS"; then  CFLAGS="$CFLAGS -g"; fi
fi

# libtool compiile
AC_SUBST(SHARED_LIB_VERSION)
ac_link="${SHELL} ${srcdir}/libtool --mode=link $ac_link"


# Checks for libraries.
AC_CHECK_LIB(m, expf)

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h stdlib.h string.h sys/file.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME
AC_CHECK_SIZEOF(int,4,,[ AC_MSG_ERROR(sizeof(int) != 4) ])
dnl AC_CHECK_SIZEOF(char *,4,,[ AC_MSG_ERROR(sizeof(char *) != 4) ])
AC_CHECK_SIZEOF(float,4,,[ AC_MSG_ERROR(sizeof(float) != 4) ])

# Checks for library functions.
AC_FUNC_LSTAT
AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_STAT
AC_CHECK_FUNCS([gettimeofday memset munmap pow realpath strcasecmp strchr strdup strrchr strndup strtok_r])

# check func mmap
AC_ARG_ENABLE(mmap, AC_HELP_STRING([--disable-mmap],
              [ DO NOT use mmap for dictionary. ]),,
              [
                AC_CHECK_FUNC([mmap])
                if test "x-$ac_cv_func_mmap" = "x-yes" ; then
                  AC_DEFINE(HAVE_MMAP,1,[with mmap for XDB])
                fi
              ] )

# check for struct_flock
AC_CACHE_CHECK(for struct flock,ac_cv_struct_flock,
    AC_TRY_COMPILE([
#include <unistd.h>
#include <fcntl.h>
        ],
        [struct flock x;],
        [
          ac_cv_struct_flock=yes
        ],[
          ac_cv_struct_flock=no
        ])
)
if test "$ac_cv_struct_flock" = "yes" ; then
    AC_DEFINE(HAVE_STRUCT_FLOCK, 1,[whether you have struct flock])
fi

# check the scws_version in scws.h
echo "/* automatically generated by configure */" > scws_version.h.new
echo "/* edit configure.in to change version number */" >> scws_version.h.new
echo "#define SCWS_VERSION \"$VERSION\"" >> scws_version.h.new
echo "#define SCWS_BUGREPORT \"$PACKAGE_BUGREPORT\"" >> scws_version.h.new
cmp scws_version.h.new $srcdir/libscws/scws_version.h >/dev/null 2>&1
if test $? -ne 0 ; then
    rm -f $srcdir/libscws/scws_version.h && mv scws_version.h.new $srcdir/libscws/scws_version.h && \
    echo 'Updated libscws/scws_version.h'
else
    rm -f scws_version.h.new
fi

AC_CONFIG_FILES([Makefile libscws/Makefile etc/Makefile cli/Makefile])
AC_OUTPUT

